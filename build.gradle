plugins {
    id "io.codearte.nexus-staging" version "0.30.0"
}

apply plugin: 'io.codearte.nexus-staging'

ext {
    // Minimum required Testerra version
    testerraCompileVersion = '1.9'
    // Unit tests use the latest Testerra version
    testerraTestVersion = '[1.9,2-SNAPSHOT)'
    moduleVersion = '1-SNAPSHOT'
    if (System.properties.containsKey('moduleVersion')) {
        moduleVersion = System.getProperty('moduleVersion')
    }
    if (System.properties.containsKey('ttVersion')) {
        testerraTestVersion = testerraCompileVersion = System.getProperty('ttVersion')
    }
}

allprojects {
    apply plugin: 'java-library'

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

subprojects {

    group 'io.testerra'
    version moduleVersion

    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    // important!
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    test {
        useTestNG()

        testLogging {
            outputs.upToDateWhen { false }
            showStandardStreams = true
        }

        options {
            systemProperties(System.getProperties())
        }

        ignoreFailures = true
    }

    tasks.whenTaskAdded { t ->
        if (t.name.startsWith("publish")) {
            boolean e = t.project.publish.enabled
            if (!e) {
                println "Not running publish for " + t.project.name
                t.enabled = false
            }
        }
    }
}

// Do not move this integration because `group` and `version` is needed for publishing
apply from: rootProject.file('publish.gradle')
