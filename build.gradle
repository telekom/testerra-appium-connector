plugins {
    id "org.danilopianini.publish-on-central" version "9.0.2"
}

ext {
    // Tested with
    appiumJavaClientVersion = '9.2.3'

    // Minimum required Testerra version
    testerraCompileVersion = '2.8'
    // Unit tests use the latest Testerra version
//    testerraTestVersion = '[2,3-SNAPSHOT)'
    testerraTestVersion = '2.11'
    moduleVersion = '2-SNAPSHOT'
    if (System.properties.containsKey('moduleVersion')) {
        moduleVersion = System.getProperty('moduleVersion')
    }
    if (System.properties.containsKey('ttVersion')) {
        testerraTestVersion = testerraCompileVersion = System.getProperty('ttVersion')
    }
}

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'

    group 'io.testerra'
    version moduleVersion

    repositories {
        mavenCentral()
    }

    test {
        useTestNG()

        testLogging {
            outputs.upToDateWhen { false }
            showStandardStreams = true
        }

        options {
            systemProperties(System.getProperties())
        }

        ignoreFailures = true
    }

}

// Deactivate JAR publishing for the root project
project.afterEvaluate {
    println "Deactivate publishing for " + project.name
    project.tasks.each { t ->
        if (t.name.startsWith("publish")) {
            t.enabled = false
        }
    }
}


// Do not move this integration because `group` and `version` is needed for publishing
apply from: rootProject.file('publish.gradle')
