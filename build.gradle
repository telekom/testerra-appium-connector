plugins {
    id "io.codearte.nexus-staging" version "0.30.0"
}

apply plugin: 'io.codearte.nexus-staging'

ext {
    // Tested with
    appiumJavaClientVersion = '9.2.3'
//    appiumJavaClientVersion = '8.6.0'

    // Minimum required Testerra version
    testerraCompileVersion = '2.8'
    // Unit tests use the latest Testerra version
//    testerraTestVersion = '[2,3-SNAPSHOT)'
    testerraTestVersion = '2.11'
    moduleVersion = '2-SNAPSHOT'
    if (System.properties.containsKey('moduleVersion')) {
        moduleVersion = System.getProperty('moduleVersion')
    }
    if (System.properties.containsKey('ttVersion')) {
        testerraTestVersion = testerraCompileVersion = System.getProperty('ttVersion')
    }
}

allprojects {
    apply plugin: 'java-library'

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'
}

subprojects {

    group 'io.testerra'
    version moduleVersion

    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    repositories {
        mavenCentral()
    }

    test {
        useTestNG()

        testLogging {
            outputs.upToDateWhen { false }
            showStandardStreams = true
        }

        options {
            systemProperties(System.getProperties())
        }

        ignoreFailures = true
    }

    tasks.whenTaskAdded { t ->
        if (t.name.startsWith("publish")) {
            boolean e = t.project.publish.enabled
            if (!e) {
                println "Not running publish for " + t.project.name
                t.enabled = false
            }
        }
    }
}

// Do not move this integration because `group` and `version` is needed for publishing
apply from: rootProject.file('publish.gradle')
